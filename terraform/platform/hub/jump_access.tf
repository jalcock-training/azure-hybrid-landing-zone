# ------------------------------------------------------------
# Jump ACI
# ------------------------------------------------------------
module "jump_aci" {
  source = "../../modules/jump-aci"

  location            = var.location
  resource_group_name = azurerm_resource_group.hub.name
  subnet_id           = module.hub_network.subnet_ids["aci"]

  container_name  = "jump-aci"
  container_image = "ghcr.io/jalcock-training/jump-aci:latest"


  cpu_cores       = 1
  memory_gb       = 1

  # Pass private key securely into ACI
  private_key_openssh = tls_private_key.jump.private_key_openssh
  jumphost_name         = module.jumphost_vm.jumphost_name

  tags = var.tags
}

# ------------------------------------------------------------
# Jumphost VM 
# ------------------------------------------------------------
module "jumphost_vm" {
  source = "../../modules/jumphost-vm"

  location            = var.location
  resource_group_name = azurerm_resource_group.hub.name
  subnet_id           = module.hub_network.subnet_ids["jumphost"]
  subscription_id = var.subscription_id

  admin_username    = "azureuser"
  # Inject public key generated by Terraform
  ssh_public_key    = tls_private_key.jump.public_key_openssh
  ssh_private_key   = tls_private_key.jump.private_key_pem
  relay_private_key = tls_private_key.relay.private_key_pem

  jumphost_size = "Standard_D2as_v5"

  tags = var.tags
}

# For access to tfstate so jumphost vm can be run terraform using delegated identity

data "azurerm_storage_account" "tfstate" {
  name                = var.tfstate_storage_account_name
  resource_group_name = var.tfstate_resource_group_name
}

data "azurerm_subscription" "current" {}

resource "azurerm_role_assignment" "jumphost_tfstate_access" {
  scope                = data.azurerm_storage_account.tfstate.id
  role_definition_name = "Storage Blob Data Contributor"
  principal_id         = module.jumphost_vm.identity_principal_id
}

# ------------------------------------------------------------
# Generate SSH keypair for ACI → VM access
# ------------------------------------------------------------
resource "tls_private_key" "jump" {
  algorithm = "RSA"
  rsa_bits  = 4096
  ecdsa_curve = null
}

# ------------------------------------------------------------
# RBAC: Allow ACI to start/stop the VM
# ------------------------------------------------------------
resource "azurerm_role_assignment" "aci_vm_control" {
  scope                = module.jumphost_vm.jumphost_id
  role_definition_name = "Virtual Machine Contributor"
  principal_id         = module.jump_aci.identity_principal_id
}

# ------------------------------------------------------------
# Generate SSH keypair for jumphost → hybrid relay access
# ------------------------------------------------------------
resource "tls_private_key" "relay" {
  algorithm = "RSA"
  rsa_bits  = 4096
}
